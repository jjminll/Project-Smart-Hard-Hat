#### 3주차: 하드웨어 개발 및 데이터베이스 구축

#### - ESP32 및 BLE 모듈 연결 및 테스트

#### - BLE를 통한 데이터 송수신 테스트

#### - 착용 감지 기능 구현 및 데이터 로깅

****

##### ESP 32 및 모듈 연결 

![KakaoTalk_20250407_143144840_04](https://github.com/user-attachments/assets/40811dae-cb6f-416d-b800-15297f9c68d9)


##### BLE 모듈을 통한 데이터 송수신 CODE

```cpp
#include <WiFi.h>              // ESP32 라이브러리
#include <PubSubClient.h>
#include <BLEDevice.h>
#include <BLEServer.h>
#include <BLEUtils.h>
#include <BLE2902.h>
#include <BLEBeacon.h>

#define DEVICE_NAME         "SmartHardHat"
#define SERVICE_UUID        "7A0247E7-8E88-409B-A959-AB5092DDB03E"
#define BEACON_UUID         "2D7A9F0C-E0E8-4CC9-A71B-A21DB2D034A1"
#define BEACON_UUID_REV     "A134D0B2-1DA2-1BA7-C94C-E8E00C9F7A2D"
#define CHARACTERISTIC_UUID "82258BAA-DF72-47E8-99BC-B73D7ECD08A5"

// BLE 비콘
BLEServer* pServer;
BLECharacteristic* pCharacteristic;
bool deviceConnected = false;
uint8_t BLEvalue = 0;

WiFiClient espClient;
PubSubClient client(espClient);
String lastMsg = "";
#define MSG_BUFFER_SIZE (50)
char msg[MSG_BUFFER_SIZE];
int value = 0;

// 테스크 핸들 정의
TaskHandle_t xEmergencyBuzzHandle = NULL;

// 인터넷 공유기 접속
void setup_wifi() {
  delay(10);
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

// 데이터 수신
void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  for (int i = 0; i < length; i++) {
    Serial.print((char)payload[i]);
  }
  Serial.println();
}

// BLE 서버 콜백 클래스
class MyServerCallbacks : public BLEServerCallbacks {
  void onConnect(BLEServer* pServer) {
    deviceConnected = true;
    Serial.println("deviceConnected = true"); // 연결됐을 때 콘솔 출력
  };

  void onDisconnect(BLEServer* pServer) {
    deviceConnected = false;
    Serial.println("deviceConnected = false"); // 연결 끊겼을 때 콘솔 출력

    // 광고 재시작해서 다시 연결되도록 설정
    BLEAdvertising* pAdvertising = pServer->getAdvertising();
    pAdvertising->start();
    Serial.println("iBeacon advertising restarted"); // 광고 재시작 메시지 출력
  }
};

// BLE 특성 콜백 클래스
class MyCallbacks : public BLECharacteristicCallbacks {
  void onWrite(BLECharacteristic* pCharacteristic) override {
    String rxValue = pCharacteristic->getValue(); // 수신된 값 가져오기

    if (rxValue.length() > 0) {
      Serial.println("*********");
      Serial.print("Received Value: ");
      for (int i = 0; i < rxValue.length(); i++) {
        Serial.print(rxValue[i]); // 수신된 값 하나씩 출력
      }
      Serial.println();
      Serial.println("*********");
    }
  }
};

// BLE 서비스 초기화
void init_service() {
  BLEAdvertising* pAdvertising = pServer->getAdvertising();
  pAdvertising->stop(); // 광고 중지

  // Create the BLE Service
  BLEService* pService = pServer->createService(BLEUUID(SERVICE_UUID));

  // Create a BLE Characteristic
  pCharacteristic = pService->createCharacteristic(
    CHARACTERISTIC_UUID,
    BLECharacteristic::PROPERTY_READ |
    BLECharacteristic::PROPERTY_WRITE |
    BLECharacteristic::PROPERTY_NOTIFY
  );

  pCharacteristic->setCallbacks(new MyCallbacks()); // 콜백 설정
  pCharacteristic->addDescriptor(new BLE2902());     // 디스크립터 추가
  pAdvertising->addServiceUUID(BLEUUID(SERVICE_UUID));

  // Start the service and advertising
  pService->start();
  pAdvertising->start(); // 광고 시작
}

// 비콘 초기화
void init_beacon() {
  BLEAdvertising* pAdvertising = pServer->getAdvertising();
  pAdvertising->stop();

  // iBeacon 설정
  BLEBeacon myBeacon;
  myBeacon.setManufacturerId(0x4c00);
  myBeacon.setMajor(5);
  myBeacon.setMinor(88);
  myBeacon.setSignalPower(0xc5);
  myBeacon.setProximityUUID(BLEUUID(BEACON_UUID_REV));

  BLEAdvertisementData advertisementData;
  advertisementData.setFlags(0x1A);
  advertisementData.setManufacturerData(myBeacon.getData());
  pAdvertising->setAdvertisementData(advertisementData);

  pAdvertising->start();
}
```
